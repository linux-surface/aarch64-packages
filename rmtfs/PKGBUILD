# Maintainer: Maximilian Luz <luzmaximilian@gmail.com>

pkgname=rmtfs
pkgver=v1.1.1.r6.gf9847a7
pkgrel=1
pkgdesc="Qualcomm Remote Filesystem service"
arch=('armv7h' 'aarch64' 'x86_64')
url="https://github.com/linux-msm/rmtfs"
license=('BSD')
makedepends=('git' 'qmic')
depends=('qrtr')
source=(
	'git+https://github.com/linux-msm/rmtfs.git'
    "0001-rmtfs-Fix-command-line-argument-parsing-for-o-optarg.patch"
)
sha256sums=('SKIP'
            '44ea5652f9379715dc8fd4c35e7a052fb79436097ba4572b82f041c56b1bcb00')

pkgver() {
	cd "$pkgname"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "rmtfs"

    git apply "$srcdir/0001-rmtfs-Fix-command-line-argument-parsing-for-o-optarg.patch"
}

build() {
	cd "$pkgname"
	make
}

package() {
	cd "$pkgname"
	make DESTDIR="$pkgdir/" prefix="/usr" install
}
